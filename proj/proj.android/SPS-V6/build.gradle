apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file('SPS-V6.keystore')
            storePassword 'quickv6'
            keyAlias 'SPS-V6'
            keyPassword 'quickv6'
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.cocos2dx.quickv6"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "0.1"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name.equals('release')) {
                def flavor = ""

                if (variant.productFlavors[0] != null) {
                    flavor = ".${variant.productFlavors[0].name}"
                }

                def ts = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))

                def filename = "${project.name}.v${defaultConfig.versionName}.${defaultConfig.versionCode}.${ts}${flavor}.apk"
                output.outputFile = new File(output.outputFile.parent, filename)
            }
        }
    }
}

task buildLUA(type: Exec) {
    workingDir '.'
    executable './build.sh'
    args('quickv6', 'QuickV6')
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.dependsOn 'buildLUA'
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.1.0'
    compile project(':libcocos2dx')
    compile project(':libiap')
    compile project(':libumeng')
    compile project(':libvungle')
}
